@file:Suppress("VulnerableLibrariesLocal")

import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import java.net.URI

plugins {
    id("java")
    id("org.jetbrains.kotlin.jvm") version "1.9.25"
    id("org.jetbrains.intellij.platform") version "2.3.0"
}

group = "org.javamaster"
version = "5.8.4"

repositories {
    maven { url = URI("https://maven.aliyun.com/nexus/content/groups/public/") }
    mavenLocal()
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    intellijPlatform {
        create("IC", "2024.3")
        testFramework(org.jetbrains.intellij.platform.gradle.TestFrameworkType.Platform)
        bundledPlugin("com.intellij.java")
        bundledPlugin("com.intellij.modules.json")
        bundledModule("com.intellij.modules.json")
        plugin("ris58h.webcalm", "0.12")
    }

    implementation("org.mozilla:rhino:1.7.15")
    implementation("com.github.javafaker:javafaker:1.0.2")
    implementation("com.jayway.jsonpath:json-path:2.9.0")

    implementation("com.alibaba:dubbo:2.6.12") {
        exclude(group = "org.springframework", module = "spring-context")
        exclude(group = "org.javassist", module = "javassist")
        exclude(group = "org.jboss.netty", module = "netty")
    }

    testImplementation("junit:junit:4.13.1")
}

sourceSets["main"].java.srcDirs("src/main/gen")

intellijPlatform {
    pluginConfiguration {
        ideaVersion {
            sinceBuild = "230"
            untilBuild = "252.*"
        }
    }
}

tasks {
    withType<JavaCompile> {
        sourceCompatibility = "17"
        targetCompatibility = "17"
        options.encoding = "UTF-8"
    }
    kotlin {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_17)
        }
    }

    runIde {
        autoReload = false
    }

    jar {
        // kt files are somehow being compiled twice due to unknown configuration, so this is a temporary fix
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    signPlugin {
        certificateChain.set(System.getenv("CERTIFICATE_CHAIN"))
        privateKey.set(System.getenv("PRIVATE_KEY"))
        password.set(System.getenv("PRIVATE_KEY_PASSWORD"))
    }

    publishPlugin {
        token.set(System.getenv("PUBLISH_TOKEN"))
    }
}
